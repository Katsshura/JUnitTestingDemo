bootJar {
    enabled = true
}

jar {
    enabled = false
}

bootRun {
    args = ["--spring.profiles.active=dev"]
}

dependencies {
    implementation project(':core')

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "javax.xml.bind:jaxb-api:${jaxApiVersion}"
    implementation "com.sun.xml.bind:jaxb-core:${jaxCoreVersion}"
    implementation "com.sun.xml.bind:jaxb-impl:${jaxImpVersion}"
    implementation "javax.validation:validation-api:${jakartaValidationVersion}"

    testImplementation project(':core')
    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testContainersVersion}"
    testImplementation "org.flywaydb.flyway-test-extensions:flyway-spring-test:${flywaySpringTestVersion}"

}

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.defaultComponentModel=spring'
    ]
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)" as Object
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

test {
    useJUnitPlatform()
}